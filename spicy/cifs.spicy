module CIFS;

import zeek;
import spicy;


public type Packet = unit {
                   : bytes &until=b"\\MAILSLOT\\";
                   
    mailSlotName  : bytes &until=b"\x00";

    opcode        : uint8 &convert=Opcode($$);

    switch ( self.opcode ) {
        Opcode::HostAnnouncement        -> hostAnnouncement:        HostAnnouncement;
        Opcode::LocalMasterAnnouncement -> localMasterAnnouncement: LocalMasterAnnouncement;
        Opcode::DomainWorkgroup         -> domainWorkgroup:         DomainWorkgroup;
        Opcode::RequestAnnouncement     -> requestAnnouncement:     RequestAnnouncement;
        Opcode::QueryForPDC             -> queryForPDC:             QueryForPDC;
        *                               -> unk:                     Unknown;
    };
    # on %done { print self; }
};

type Opcode = enum {
    HostAnnouncement        = 0x01,
    LocalMasterAnnouncement = 0x0f,
    DomainWorkgroup         = 0x0c,
    RequestAnnouncement     = 0x02,
    QueryForPDC             = 0x07
};

type HostAnnouncement = unit {
    update_count        : bytes &size=1;
    update_periodicity  : bytes &size=4;
    serverName          : bytes &size=16;
    osVersion_1         : uint8;
    osVersion_2         : uint8;

    serverType          : bitfield(32) {
        x1: 0..7;
        x2: 8..15;
        x3: 16..23;
        x4: 24..31;
    } &byte-order=spicy::ByteOrder::Little;

    browserVersion_1    : uint8;
    browserVersion_2    : uint8;
    signature_1         : bytes &size=1;
    signature_2         : bytes &size=1;
    hostComment         : bytes &eod;
};

type LocalMasterAnnouncement = unit {
    update_count        : bytes &size=1;
    update_periodicity  : bytes &size=4;
    serverName          : bytes &size=16;
    osVersion_1         : uint8;
    osVersion_2         : uint8;

    serverType          : bitfield(32) {
        x1: 0..7;
        x2: 8..15;
        x3: 16..23;
        x4: 24..31;
    } &byte-order=spicy::ByteOrder::Little;

    browserVersion_1    : uint8;
    browserVersion_2    : uint8;
    signature_1         : bytes &size=1;
    signature_2         : bytes &size=1;
    hostComment         : bytes &eod;
};

type DomainWorkgroup = unit {
    update_count        : bytes &size=1;
    update_periodicity  : bytes &size=4;
    domain_workgroup    : bytes &size=16;
    osVersion_1         : uint8;
    osVersion_2         : uint8;

    serverType          : bitfield(32) {
        x1: 0..7;
        x2: 8..15;
        x3: 16..23;
        x4: 24..31;
    } &byte-order=spicy::ByteOrder::Little;

    mysteriousField     : bytes &size=4;
    master_browser      : bytes &eod;
};

type RequestAnnouncement = unit {
    unusedFlags : bytes &size=1;
    computerName: bytes &eod;
};

type QueryForPDC = unit {
    opcode_hi             : bytes &size = 1;
    computerName          : bytes &until=b"\x00";      # ASCII, NUL-terminated
    payload_mailslot      : bytes &until=b"\x00";      # ASCII, NUL-terminated (Ã¶r. "\\MAILSLOT\\NET\\GETDC146")
    unicode_computer_name : bytes &size = 22; ##??
    ntVersion             : uint32 &byte-order=spicy::ByteOrder::Little;
    lmntToken             : bytes &size=2;
    lm20Token             : bytes &eod;
};

type Unknown = unit {
    data: bytes &eod;
};
